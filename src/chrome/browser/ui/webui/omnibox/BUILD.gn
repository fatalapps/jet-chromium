# Copyright 2014 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")

mojom("mojo_bindings") {
  sources = [ "omnibox_internals.mojom" ]
  webui_module_path = "/chrome/browser/ui/webui/omnibox"

  deps = [ "//url/mojom:url_mojom_gurl" ]

  cpp_typemaps = [
    {
      types = [
        {
          mojom = "mojom.ACMatchClassification"
          cpp = "::AutocompleteMatch::ACMatchClassification"
        },
        {
          mojom = "mojom.AutocompleteMatch"
          cpp = "::AutocompleteMatchWrapper"
        },
        {
          mojom = "mojom.Signals"
          cpp = "::AutocompleteMatch::ScoringSignals"
        },
      ]
      traits_headers = [ "omnibox_mojom_traits.h" ]

      traits_public_deps = [ ":mojom_traits" ]
    },
  ]
}

source_set("mojom_traits") {
  sources = [
    "omnibox_mojom_traits.cc",
    "omnibox_mojom_traits.h",
  ]

  deps = [ "//ui/base" ]

  public_deps = [
    ":mojo_bindings_shared_cpp_sources",
    "//base",
    "//components/bookmarks/browser",
    "//components/omnibox/browser",
  ]
}

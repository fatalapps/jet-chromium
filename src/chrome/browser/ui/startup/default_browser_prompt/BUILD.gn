# Copyright 2025 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/args.gni")

source_set("default_browser_prompt") {
  sources = []
  public_deps = []

  if (!is_android) {
    sources += [
      "default_browser_infobar_delegate.h",
      "default_browser_prompt_manager.h",
      "default_browser_prompt_prefs.h",
    ]
    public_deps += [
      "//base",
      "//chrome/browser:browser_process",
      "//chrome/browser:shell_integration",
      "//chrome/browser/profiles:profile",
      "//chrome/browser/ui:browser_list",
      "//chrome/browser/ui:browser_tab_strip",
      "//chrome/browser/ui/tabs:tab_model",
      "//chrome/browser/ui/tabs:tab_strip_model_observer",
      "//components/infobars/content",
      "//components/infobars/core",
      "//content/public/browser",
    ]
  }
  if (is_win || is_mac || is_linux) {
    sources += [ "default_browser_prompt.h" ]
  }
}

source_set("impl") {
  sources = []
  deps = [ ":default_browser_prompt" ]
  public_deps = [ "//chrome/browser:browser_public_dependencies" ]

  if (!is_android) {
    sources += [
      "default_browser_infobar_delegate.cc",
      "default_browser_prompt_manager.cc",
      "default_browser_prompt_prefs.cc",
    ]

    deps += [
      "//chrome/app:branded_strings",
      "//chrome/app:generated_resources",
      "//chrome/browser:shell_integration",
      "//chrome/browser/infobars",
      "//chrome/browser/ui:ui_features",
      "//chrome/browser/ui/browser_window",
      "//components/infobars/core",
      "//components/omnibox/browser",
      "//components/omnibox/browser:vector_icons",
      "//components/prefs",
      "//components/vector_icons",
      "//content/public/common",
      "//ui/base",
    ]

    if (is_win) {
      deps += [ "//chrome/installer/util:with_no_strings" ]
    }
  }
  if (is_win || is_mac || is_linux) {
    sources += [ "default_browser_prompt.cc" ]

    deps += [
      "//chrome/browser/ui/tabs:tab_strip",
      "//components/infobars/content",
      "//components/version_info",
    ]
  }
}

source_set("unit_tests") {
  testonly = true
  sources = []
  deps = []

  if (is_win || is_mac || is_linux) {
    sources += [
      "default_browser_prompt_manager_unittest.cc",
      "default_browser_prompt_unittest.cc",
    ]
    deps += [
      ":default_browser_prompt",
      "//base",
      "//chrome/browser:browser_process",
      "//chrome/browser/ui:browser_list",
      "//chrome/common:constants",
      "//chrome/test:test_support",
      "//components/infobars/content",
      "//components/prefs",
      "//components/prefs:test_support",
      "//content/test:test_support",
      "//testing/gmock",
      "//testing/gtest",
    ]
  }
}

if (!is_android && !is_chromeos_device) {
  source_set("interactive_ui_tests") {
    testonly = true
    defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
    sources = [ "default_browser_prompt_interactive_uitest.cc" ]
    deps = [
      ":default_browser_prompt",
      "//base/test:test_support",
      "//chrome/app:branded_strings",
      "//chrome/app:command_ids",
      "//chrome/browser:browser_process",
      "//chrome/browser:shell_integration",
      "//chrome/browser/profiles:profile",
      "//chrome/browser/ui:browser_element_identifiers",
      "//chrome/browser/ui/startup",
      "//chrome/browser/ui/toolbar",
      "//chrome/browser/ui/views/infobars",
      "//chrome/common",
      "//chrome/common:constants",
      "//chrome/common:non_code_constants",
      "//chrome/test:test_support",
      "//chrome/test:test_support_ui",
      "//components/infobars/content",
      "//components/infobars/core",
      "//components/prefs",
      "//content/public/browser",
      "//content/public/common",
      "//content/test:test_support",
      "//testing/gtest",
      "//ui/base",
      "//ui/gfx:gfx_switches",
      "//ui/gfx:test_support",
    ]
  }
}

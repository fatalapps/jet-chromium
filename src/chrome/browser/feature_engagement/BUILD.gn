# Copyright 2021 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/browser/buildflags.gni")

if (is_android) {
  import("//build/config/android/config.gni")
  import("//build/config/android/rules.gni")
  import("//third_party/jni_zero/jni_zero.gni")

  android_library("java") {
    srcjar_deps = [ ":jni_headers" ]
    sources = [ "java/src/org/chromium/chrome/browser/feature_engagement/TrackerFactory.java" ]

    deps = [
      "//base:base_java",
      "//chrome/browser/profiles/android:java",
      "//components/feature_engagement/public:public_java",
      "//third_party/androidx:androidx_annotation_annotation_java",
      "//third_party/androidx:androidx_core_core_java",
      "//third_party/jni_zero:jni_zero_java",
    ]
  }

  generate_jni("jni_headers") {
    sources = [ "java/src/org/chromium/chrome/browser/feature_engagement/TrackerFactory.java" ]
  }
}

source_set("feature_engagement") {
  sources = [ "tracker_factory.h" ]
  public_deps = [
    "//chrome/browser/profiles",
    "//components/feature_engagement/public",
  ]
}

source_set("impl") {
  sources = [ "tracker_factory.cc" ]
  deps = [
    ":feature_engagement",
    "//base",
    "//chrome/common:constants",
    "//chrome/common:non_code_constants",
    "//components/prefs",
    "//content/public/browser",
  ]

  if (!is_android) {
    deps += [ "//chrome/browser/user_education" ]
  }

  if (is_chromeos) {
    deps += [ "//chromeos/ash/components/growth:config_provider" ]
  }

  public_deps = [
    "//chrome/browser/profiles",
    "//components/feature_engagement/public",
  ]
}
